apply plugin: 'com.android.library'
apply from: file(rootProject.file('module.gradle'))

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    ndkVersion '25.2.9519653'
    
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        externalNativeBuild {
            cmake {
                arguments "-DMODULE_NAME:STRING=$moduleLibraryName"
                abiFilters 'arm64-v8a' 
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
}

afterEvaluate {
    android.libraryVariants.forEach { variant ->
        def variantCapped = variant.name.capitalize()
        def variantLowered = variant.name.toLowerCase()
        def zipName = "${magiskModuleId.replace('_', '-')}-${moduleVersion}-${variantLowered}.zip"
        def magiskDir = file("$outDir/magisk_module_$variantLowered")

        task("prepareMagiskFiles${variantCapped}", type: Sync) {
            dependsOn("assemble$variantCapped")
            def templatePath = "$rootDir/template/magisk_module"
            into magiskDir
            from(templatePath) {
                include 'module.prop'
                expand([id: magiskModuleId, name: moduleName, version: moduleVersion, versionCode: moduleVersionCode.toString(), author: moduleAuthor, description: moduleDescription])
            }
            from(templatePath) { exclude 'module.prop' }

            from("$buildDir/intermediates/stripped_native_libs/$variantLowered/out/lib") { into 'lib' }

            doLast {
                def zygiskDir = file("$magiskDir/zygisk")
                zygiskDir.mkdirs()
                def myLib = file("$magiskDir/lib/arm64-v8a/lib${moduleLibraryName}.so")
                if(myLib.exists()){
                    // 只移动插件本体
                    java.nio.file.Files.move(myLib.toPath(), java.nio.file.Paths.get(zygiskDir.absolutePath, "arm64-v8a.so"))
                }
                file("$magiskDir/lib").deleteDir()
            }
        }

        task("zip${variantCapped}", type: Zip) {
            dependsOn("prepareMagiskFiles${variantCapped}")
            from magiskDir
            archiveFileName.set(zipName)
            destinationDirectory.set(outDir)
        }
        variant.assembleProvider.get().finalizedBy("zip${variantCapped}")
    }
}
