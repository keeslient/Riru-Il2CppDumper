import org.apache.tools.ant.filters.FixCrLfFilter
import java.nio.file.Paths
import java.nio.file.Files

apply plugin: 'com.android.library'
apply from: file(rootProject.file('module.gradle'))

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    ndkVersion '25.2.9519653'
    
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        externalNativeBuild {
            cmake {
                arguments "-DMODULE_NAME:STRING=$moduleLibraryName"
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
}

afterEvaluate {
    android.libraryVariants.forEach { variant ->
        def variantCapped = variant.name.capitalize()
        def variantLowered = variant.name.toLowerCase()
        def zipName = "${magiskModuleId.replace('_', '-')}-${moduleVersion}-${variantLowered}.zip"
        def magiskDir = file("$outDir/magisk_module_$variantLowered")

        task("prepareMagiskFiles${variantCapped}", type: Sync) {
            dependsOn("assemble$variantCapped")
            def templatePath = "$rootDir/template/magisk_module"
            into magiskDir
            from(templatePath) { exclude 'module.prop' }
            from(templatePath) {
                include 'module.prop'
                expand([
                        id: magiskModuleId, name: moduleName, version: moduleVersion,
                        versionCode: moduleVersionCode.toString(), author: moduleAuthor, description: moduleDescription
                ])
                filter(FixCrLfFilter.class, eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }

            from("$buildDir/intermediates/stripped_native_libs/$variantLowered/out/lib") { into 'lib' }

            doLast {
                def zygiskDir = file("$magiskDir/zygisk")
                zygiskDir.mkdirs()
                fileTree("$magiskDir/lib").visit { f ->
                    if (f.directory) return
                    def abi = f.file.parentFile.name
                    if (f.name == "lib${moduleLibraryName}.so") {
                        Files.move(f.file.toPath(), Paths.get(zygiskDir.absolutePath, "${abi}.so"))
                    }
                }
                file("$magiskDir/lib").deleteDir()
            }
        }

        task("zip${variantCapped}", type: Zip) {
            dependsOn("prepareMagiskFiles${variantCapped}")
            from magiskDir
            archiveFileName.set(zipName)
            destinationDirectory.set(outDir)
        }
        variant.assembleProvider.get().finalizedBy("zip${variantCapped}")
    }
}
